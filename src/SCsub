import os

Import('env')

#absolute paths so this builds correctly in godot
src_base = Dir('.').abspath
src_dirs = [
    ".",
    "box2d", "box2d/Include", "box2d/Source/Collision", "box2d/Source/Common", "box2d/Source/Dynamics", "box2d/Source/Dynamics/Contacts", "box2d/Source/Dynamics/Joints",
    "glib", 
    "glib_adapter",
    "openlibm", 
    "sim", 
    "softfloat", 
    "spectre"
]
src_dirs_abs = [os.path.join(src_base, d) for d in src_dirs]

env.Append(CPPPATH=src_dirs_abs)

sources = []
for dir in src_dirs_abs:
    sources += Glob(f"{dir}/*.cpp")
    sources += Glob(f"{dir}/*.c")

if env.get('godot', False):
    # modules_sources only exists in godot
    env.add_source_files(env.modules_sources, sources)
else:
    # Puts static lib in bin/, but leaves object files with source
    lib = env.StaticLibrary(target='#bin/ftlib', source=sources)
    Default(lib)