Import('env')

src_dirs = [
    ".",
    "box2d", "box2d/Include", "box2d/Source/Collision", "box2d/Source/Common", "box2d/Source/Dynamics", "box2d/Source/Dynamics/Contacts", "box2d/Source/Dynamics/Joints",
    "glib", 
    "glib_adapter",
    "openlibm", 
    "sim", 
    "softfloat", 
    "spectre"
]

menv = env.Clone()

if not menv.get('godot', False):
    src_dirs.append('cli_adapter')

menv.Append(CPPPATH=src_dirs)

sources = []
for dir in src_dirs:
    sources += Glob(f"{dir}/*.cpp")
    sources += Glob(f"{dir}/*.c")

if menv.get('godot', False):
    # modules_sources only exists in godot
    menv.modules_sources += sources
else:
    # Puts static lib in bin/, but leaves object files with source
    lib = menv.StaticLibrary(target='#bin/ftlib', source=sources)
    Default(lib)